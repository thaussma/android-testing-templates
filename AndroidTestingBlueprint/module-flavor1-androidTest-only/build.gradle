apply plugin: 'com.android.test' // A plugin used for test-only-modules

android {
    compileSdkVersion 22
    buildToolsVersion = '23.0.0'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
        // The package name of the test app
        testApplicationId 'com.example.android.testing.blueprint.test'
        // The Instrumentation test runner used to run tests.
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    // Set the target app project. The module specified here should contain the production code
    // test should run against.
    targetProjectPath ':app'
    targetVariant 'flavor1Debug'
}

dependencies {
    // Android Testing Support Library's runner and rules and hamcrest matchers
    compile 'com.android.support.test:runner:0.3'
    compile 'com.android.support.test:rules:0.3'
    compile 'org.hamcrest:hamcrest-core:1.3'
}

afterEvaluate {
//    app:installFlavor1Debug module-flavor1-androidTest-only:installDebug
    project.task("connectedFeatureTest") {
        dependsOn project.findProject(':app').tasks.installFlavor1Debug
        dependsOn project.tasks.installDebug

        doLast {
            Process p = "adb shell am instrument -w com.example.android.testing.blueprint.test/android.support.test.runner.AndroidJUnitRunner".
                    execute()
            p.consumeProcessOutput(System.out, System.err)
            p.waitFor()
        }
    }

}